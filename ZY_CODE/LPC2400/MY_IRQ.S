;/****************************************Copyright (c)**************************************************
;**                               广州周立功单片机发展有限公司
;**                                     研    究    所
;**                                        产品一部 
;**
;**                                 http://www.zlgmcu.com
;**
;**--------------文件信息--------------------------------------------------------------------------------
;**文   件   名: IRQ.s
;**创   建   人: 陈明计
;**最后修改日期: 2004年2月2日
;**描        述: 允许中断嵌套时的IRQ句柄
;**              每个工程应当有独立的这个文件的拷贝，并进行相应的修改   
;**--------------历史版本信息----------------------------------------------------------------------------
;** 创建人: 陈明计
;** 版  本: v1.0
;** 日　期: 2004年2月2日
;** 描　述: 原始版本
;**
;**------------------------------------------------------------------------------------------------------
;** 修改人: 
;** 版  本: 
;** 日　期: 
;** 描　述: 
;**
;**--------------当前版本修订------------------------------------------------------------------------------
;** 修改人:
;** 日　期:
;** 描　述:
;**
;**------------------------------------------------------------------------------------------------------
;********************************************************************************************************/

NoIrq        EQU     0x80
NoFiq        EQU     0x40

USR32Mode    EQU     0x10
FIQ32Mode    EQU     0x11
IRQ32Mode    EQU     0x12
SVC32Mode    EQU     0x13
ABT32Mode    EQU     0x17
UND32Mode    EQU     0x1b
SYS32Mode    EQU     0x1f

VICVectAddr EQU     0xFFFFFF00

    CODE32

    AREA    IRQ,CODE,READONLY

    MACRO
$IRQ_Label HANDLER $IRQ_Exception_Function

        EXPORT  $IRQ_Label                      ;//输出的标号
        IMPORT  $IRQ_Exception_Function         ;//引用的外部标号

$IRQ_Label
        SUB     LR, LR, #4                      ;//计算返回地址
        STMFD   SP!, {LR}                          ;//保存链接寄存器
        MRS     LR, SPSR
        STMFD   SP!, {LR}                        ;//保存状态
        
        MSR     CPSR_c, #SYS32Mode                ;//切换到系统模式(允许中断)
        STMFD   SP!, {R0-R3, R12, LR}            ;//保存任务环境
        
        BL      $IRQ_Exception_Function         ;//调用c语言的中断处理程序
        
        LDMFD   SP!, {R0-R3, R12, LR}            ;//恢复任务环境
        MSR     CPSR_c, #(NoIrq | NoFiq | IRQ32Mode)    ;//切换回irq模式
        
        LDR     LR, =0                            ;
        STR     LR, [LR, #VICVectAddr]            ;//退出前写0到VICVectAddr撤消优先级
        
        LDMFD   SP!, {LR}                       ;//恢复状态
        MSR     SPSR_cxsf, LR

        LDMFD   SP!, {PC}^                        ;//回到中断前的状态
    MEND

;/* 以下添加中断句柄，用户根据实际情况改变 */

IRQ_Timer0_Handler HANDLER IRQ_Timer0
IRQ_RTC_Handler    HANDLER IRQ_RTC
;IRQ_UART0_Handler HANDLER IRQ_UART0

    END
;/*********************************************************************************************************
;**                            End Of File
;********************************************************************************************************/
